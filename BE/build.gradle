plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
}

group = 'com.newsainturtle'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '1.8'
}

repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    runtimeOnly 'com.h2database:h2'
    implementation group: 'com.auth0', name: 'java-jwt', version: '4.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation group: 'org.objenesis', name: 'objenesis', version: '3.3'
    implementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.14.10'

}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
        excludes += ["com/newsainturtle/shadowmate/ShadowmateApplication.class",
                     "com/newsainturtle/shadowmate/common/**",
                     "com/newsainturtle/shadowmate/config/**",
                     "com/newsainturtle/shadowmate/auth/service/RedisServiceImpl.class",
                     "com/newsainturtle/shadowmate/**/enums/**",
                     "com/newsainturtle/shadowmate/**/exception/**"]
    }
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled false
        csv.enabled false
    }
    afterEvaluate {

        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["com/newsainturtle/shadowmate/ShadowmateApplication.class",
                              "com/newsainturtle/shadowmate/common/**",
                              "com/newsainturtle/shadowmate/config/**",
                              "com/newsainturtle/shadowmate/auth/service/RedisServiceImpl.class",
                              "com/newsainturtle/shadowmate/**/enums/**",
                              "com/newsainturtle/shadowmate/**/exception/**"])
        }))
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 200
            }

            excludes = ["com.newsainturtle.shadowmate.ShadowmateApplication",
                        "com.newsainturtle.shadowmate.common.*",
                        "com.newsainturtle.shadowmate.config.*",
                        "com.newsainturtle.shadowmate.auth.service.RedisServiceImpl",
                        "com.newsainturtle.shadowmate.**.enums.*",
                        "com.newsainturtle.shadowmate.**.exception.*"]
        }

    }
}